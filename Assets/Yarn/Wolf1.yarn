title: Start
---
<<jump Wolf_1>>
===

title: Wolf_1
tags:
position: -748,-1095
---
<<SetMusic Chill>>
<<SetPose Wolf Flippant>>
<<AddCharacter Wolf>>
Wolf: Well, well. A little piggy comes to pay a visit to the big bad wolf.
<<PlayEffect shock>>
Boy: Can it Wolf, We got a list of evidence a mile long. This is only a formality. But if you answer my questions, maybe we can let you off a bit easier this time.
<<PlayEffect interesting>>
Wolf: You're not getting <style="emph">anything</style> out of me.
<<PlayEffect emphasis>>

<<jump Wolf_Q1_Start>>
===
title: Wolf_Q1_Start
tags:
position: -749,-923
---
<<SetPose Wolf CheckedOut>>
<<if visited("Wolf_Q1_Question_2") == true && visited("Wolf_Q1_Question_1") == true >>
    <<jump Wolf_Q2>>
<<else>>
    -> Question
        -> This is a new low, even for you. Selling illicit drugs to kids? What's with the shift here Wolf?
            <<jump Wolf_Q1_Question_1>>
        -> This stuff has been spreading like wildfire all over the city. Where'd you get the connections to set up a distribution network like this?
            <<jump Wolf_Q1_Question_2>>
    -> Accuse
        -> This evidence proves you wrong!
            <<ResetEvidenceResponses>>
            //<<SetEvidenceResponse testPaper_1 TestPaper1Response>>
            <<PromptEvidence Wolf_Q1_Accusation_Default>>
    -> Review
        <<Review>>
<<endif>>

===
title: Wolf_Q1_Question_1
tags:
position: -946,-784
---
<<SetPose Wolf Flippant>>
<<if visited("Wolf_Q1_Question_1") == true>>
    <<wait 0.5>>
    Wolf: Again? Did you forget what I said the first time? Check that little book of yours.
<<else>>
    //Isn't wolf trying to deny he sells drugs?
    Wolf: Wow. Great question, Sherlock. Money, Power whatever really...
    <<AddStatement wolf_q1_drugs "Wolf sells drugs for the money and power">>
<<endif>>
Boy: Alright, smart ass.
<<jump Wolf_Q1_Start>>

===
title: Wolf_Q1_Question_2
tags:
position: -538,-785
---
<<SetPose Wolf CheckedOut>>
<<if visited("Wolf_Q1_Question_2") == true>>
    <<SetPose Wolf Flippant>>
    <<wait 0.5>>
    Wolf: Again? Did you forget what I said the first time?
    Boy: I'm asking the questions here.
<<endif>>
Wolf: Simple, law of supply and demand. Demand for this stuff went up, so everyone came to the only supply: Me.
Boy: What are you even calling this stuff? Every person we've talked to has a different name for it.
Wolf: Pixie Dust, name is stupid I know..
Boy: But didn't you...ah whatever
<<jump Wolf_Q1_Start>>

===
title:  Wolf_Q1_Accusation_Default
tags:
position: -323,-926
---
Wolf: And thats relevant how? 
Wolf: You're losing your edge kid
Boy: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump Wolf_Q1_Start>>
===
title: Wolf_Q2
position: -745,-569
---
<<SetPose Wolf CheckedOut>>
Boy: Alright Wolf, I have a few more questions for you. 
Wolf: Sure, not like I can go anywhere. Can ya hurry this up, I have a shift in a few hours?
Boy: Depends on how you answer these questions Wolf, don't lie or mouth off and it will be done sooner.

<<jump Wolf_Q2_Start>>
===
title: Wolf_Q2_Start
position: -745,-405
---
<<SetPose Wolf CheckedOut>>
-> Question
    -> Where were you yesterday?
        <<jump Wolf_Q2_Question_1>>
    -> Did you leave the building at all? Any errands? <<if visited("Wolf_Q2_Question_1") == true>>
        <<jump Wolf_Q2_Question_2>>
    -> What games did you play?<<if visited("Wolf_Q2_Question_1") == true>>
        <<jump Wolf_Q2_Question_3>>
-> Accuse
    -> This evidence proves you wrong!
        <<ResetEvidenceResponses>>
        <<SetEvidenceResponse Wolf_CCTV_Image Wolf_Q2_Accusation_Correct>>
        <<PromptEvidence Wolf_Q2_Accusation_Default>>
-> Review
    <<Review>>
===
title: Wolf_Q2_Question_1
position: -1002,-284
---
Wolf: I was spending time with my kids.
Boy: Uh huh...
Wolf: They had a day off from school so they wanted to play board games.
Boy: Hmmm..
<<AddStatement wolf_q2_boardgames "Wolf played board games with his kids">>
<<jump Wolf_Q2_Start>>
===
title: Wolf_Q2_Question_2
position: -490,-292
---
Wolf: hmm...I took the trash out and got the mail.
Wolf: Think that was it, was a pretty uneventful day
Boy: I see..
<<jump Wolf_Q2_Start>>
===
title: Wolf_Q2_Question_3
position: -743,-171
---
<<SetPose Wolf Flippant>>
Wolf: What kinda question is that? How the hell is that relevant? 
Boy: Just answer the question Wolf.
Wolf: Fine, Whatever.
Wolf: We played Financial Syndicate, took forever to finish. 
Wolf: Pretty sure Linus was cheating with how many doubles he rolled. Kid nearly bankrupted me twice.
Boy: Mhmm...I see...
<<jump Wolf_Q2_Start>>
===
title: Wolf_Q2_Accusation_Correct
tags:
position: -233,-214
---
Boy: You claim that you were at home all day, yet we have this footage showing you on Main. 
Wolf: How do you know that's me? The quality on that photo is atrocious
Boy: The owner of the shop witnessed you walk by as well. Wolves are an uncommon sight lately.
//Wolf: Yeah with how badly we get treated around here thats no suprise
<<jump Wolf_Q3>>
===

title:  Wolf_Q2_Accusation_Default
tags:
position: -231,-426
---
Wolf: And thats relevant how? 
Wolf: You're losing your edge kid.
Boy: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump Wolf_Q2_Start>>
===
title: Wolf_Q3
position: -751,20
---
Boy: So lets try that again Wolf.
Boy: Where else did you go yesterday?
Wolf: I went on a walk after the kids bankrupted me. Wanted to get some fresh air.
<<jump Wolf_Q3_Start>>
===
title: Wolf_Q3_Start
position: -751,186
---
<<SetPose Wolf CheckedOut>>
-> Question
    -> You remember roughly your route?
        <<jump Wolf_Q3_Question_1>>
    -> Were you walking anywhere in particular? 
        <<jump Wolf_Q3_Question_2>>
    -> Did you talk with anyone or give anything to anyone while out? <<if visited("Wolf_Q3_Question_2") == true && visited("Wolf_Q3_Question_1") == true >>
        <<jump Wolf_Q3_Question_3>>
-> Accuse
    -> This evidence proves you wrong!
        <<ResetEvidenceResponses>>
        <<SetEvidenceResponse Wolf_MetroPass Wolf_Q3_Accusation_Correct>>
        <<PromptEvidence Wolf_Q3_Accusation_Default>>
-> Review
    <<Review>>
===
title: Wolf_Q3_Question_1
position: -1012,251
---
Wolf: Not really, wasn't really paying attention
Boy: Stuck in your own head?
Wolf: A bit, works been stressful...
Wolf: ...why the hell am I even saying this? Never mind.
<<jump Wolf_Q3_Start>>
===
title: Wolf_Q3_Question_2
position: -751,352
---
Wolf: No.
Wolf: Told you already I was just trying to get some fresh air
Boy: Hmm...
<<jump Wolf_Q3_Start>>
===
title: Wolf_Q3_Question_3
position: -504,245
---
<<SetPose Wolf Flippant>>
Wolf: OI that crossed a line.
Wolf: Are you insinuating I went out to give drugs to someone?
Boy: You said it, not me.
Wolf: Look, I may be a criminal at times. But I am a good parent. I would never ever have drugs at home. 
Wolf: I'd never do something to endanger my kids. I'd go through hell to protect them.
<<jump Wolf_Q3_Start>>
===
title: Wolf_Q3_Accusation_Default
position: -248,180
---
Wolf: And thats relevant how? 
Wolf: You're losing your edge kid.
Boy: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump Wolf_Q3_Start>>
===
title: Wolf_Q3_Accusation_Correct
position: -260,413
---
Boy: We noticed you had a metro card in your wallet when we booked you.
Boy: We had a chat with the metro authority, and they report this card was used yesterday at King Log station, not far from your residence.
Boy: So Wolf, tell me. Where did you go?
Wolf: I...You....
Boy: You better cooperate Wolf
Wolf: Fine, I went out to do some errands. 

<<jump Wolf_Q4>>
===
title: Wolf_Q4
position: -749,541
---
<<jump Wolf_Q4_Start>>
===
title: Wolf_Q4_Start
position: -747,700
---
<<SetPose Wolf Temp>>
-> Question
    -> What errands?
        <<jump Wolf_Q4_Question_1>>
    -> And whats the name of this farm? <<if visited("Wolf_Q4_Question_1") == true >>
        <<jump Wolf_Q4_Question_2>>
-> Accuse
    -> This evidence proves you wrong!
        <<ResetEvidenceResponses>>
        <<SetEvidenceResponse Wolf_Wallet Wolf_Q4_Accusation_Correct>>
        <<PromptEvidence Wolf_Q4_Accusation_Default>>
-> Review
    <<Review>>
===
title: Wolf_Q4_Question_1
position: -960,815
---
Wolf: I..uh..went to go get strawberries for my kids. They really like this one farm outside the city.
Boy: Uh huh...
<<jump Wolf_Q4_Start>>
===
title: Wolf_Q4_Question_2
position: -745,928
---
Wolf: It called...its...Uhhhhh...Name escapes me at the moment.
Boy: Wolf, don't lie to me
Wolf: I'm not, I'm not. It's out by Lindworm Mermorial Station. 
Boy: The one by those cabins that exploded last month?
Wolf: Yeah thats the place. They grow this one variety that are super juicy but last like 2 days tops.
Wolf: My kids adore them, and my wife makes jam out of the left over ones.
Boy: I see..
<<jump Wolf_Q4_Start>>
===

title: Wolf_Q4_Accusation_Correct
position: -254,1026
---
Boy: There's nothing showing you were ever there Wolf.
Boy: So how do I know you are not lying to me again?
<<SetPose Wolf Sad>>
Wolf: I...I'm not lying. You have to believe me.
Boy: Sorry Wolf, I don't.
<<jump Wolf_Q5>>
===
title: Wolf_Q4_Accusation_Default
position: -300,744
---
Wolf: And thats relevant how? 
Wolf: You're losing your edge kid.
Boy: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump Wolf_Q4_Start>>
===
title: Wolf_Q5
position: -751,1141
---
Boy: Sorry Wolf, you'll be staying here for a while
Boy: Maybe you'll tell me the truth soon
===
