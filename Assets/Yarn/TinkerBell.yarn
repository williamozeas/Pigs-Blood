title: StartTinkerBell
tags:
position: -749,-1282
---
<<SetMusic Chill>>
<<jump TinkerBell_1>>
===
title: TinkerBell_1
tags:
position: -748,-1095
---
<<SetMusic Questions>>
//<<SetPose TinkerBell Smug>>
<<AddCharacter TinkerBell>>
Shepherd: Ah, so you must be TinkerBell.
TinkerBell: Oh so you're the Wolf Shepherd huh? Bit scruffier and scrawnier than I expected.
Shepherd: And you're much more....flamboyant than I expected.
<<SetPose TinkerBell Angry>>
TinkerBell: Flamboyant? I hope you're just as bad at interrogating as you are complimenting people.
<<PlayEffect shock>>
Shepherd: That wasnt...ugh

<<jump TinkerBell_Q1_Start>>
===
title: TinkerBell_Q1_Start
tags:
position: -749,-923
---
<<SetPose TinkerBell Neutral>>
<<SetMusic Questions>>
<<if visited("TinkerBell_Q1_Question_3") == true && visited("TinkerBell_Q1_Question_1") == true >>
    <<jump TinkerBell_Q2>>
<<else>>
    -> Question
        -> So, describe your day yesterday.
            <<jump TinkerBell_Q1_Question_1>>
        -> Can you elaborate on the inventory management? <<if visited("TinkerBell_Q1_Question_1") == true >>
            <<jump TinkerBell_Q1_Question_2>>
        -> What do you mean by dispute? <<if visited("TinkerBell_Q1_Question_1") == true >>
            <<jump TinkerBell_Q1_Question_3>>
    -> Accuse
        -> This evidence proves you wrong!
            <<ResetEvidenceResponses>>
            //<<SetEvidenceResponse TinkerBell_Pig_Info TinkerBell_Q1_Accusation_Correct>>
            <<PromptEvidence TinkerBell_Q1_Accusation_Default>>
    -> Review
        <<Review>>
<<endif>>

===
title: TinkerBell_Q1_Question_1
tags:
position: -946,-784
---
<<SetPose TinkerBell Smug>>
<<if visited("TinkerBell_Q1_Question_1") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked me that, aren't you taking notes on this?
    <<jump TinkerBell_Q1_Start>>
<<else>>
    TinkerBell: Well, lets see..
    TinkerBell: I had to pay my employees for the month and did  some book balancing as well as manage inventory.
    TinkerBell: Did an inventory run for the club, took a couple of hours to get everything. Had to deal with a troublesome client that had been harrassing some staff as well.
    <<AddStatement TinkerBell_q1_alibi "TinkerBell did inventory and restocked. She also had to deal with some client issues. ">>
<<endif>>
<<jump TinkerBell_Q1_Start>>

===
title: TinkerBell_Q1_Question_2
tags:
position: -596,-733
---
<<SetPose TinkerBell Neutral>>
<<if visited("TinkerBell_Q1_Question_2") == true>>
    <<SetPose TinkerBell Smug>>
    <<wait 0.5>>
    TinkerBell: Again? Are you even listening?
    Shepherd: Quiet, I'm asking the questions here.
<<endif>>
TinkerBell: Not much to elaborate on unless you want me to bore you to death with finacial records, return estimates and projected usage rates.
Shepherd: You do that yourself? I would have guessed the boss wouldn't want to get her hands dirty with all that.
TinkerBell: Gives me something to do, and honestly it's rather relaxing. And besides, less hands involved means less potential product disappearance.
Shepherd: What kind of stuff would be disappearing at a club?
TinkerBell: Mostly booze and the like. I worry mostly about any of the stage effects going missing. 
Shepherd: Hmm..I see

<<jump TinkerBell_Q1_Start>>

===
title: TinkerBell_Q1_Question_3
position: -746,-638
---
<<SetPose TinkerBell Smug>>
<<if visited("TinkerBell_Q1_Question_1") == true>>
    <<wait 0.5>>
    TinkerBell: Am I going crazy? I could have sworn you wrote that down.
    <<jump TinkerBell_Q1_Start>>
<<else>>
    TinkerBell: I can't really get into the details, since I doubt they would be happy if I explained the full story.
    TinkerBell: This client has a tendency to put everything on a tab every time they come in. Well, this was the third time they left it open at the end of the night without paying.
    TinkerBell: So you know, I showed up at his house and asked him to pay me then and there or he wouldn't be allowed back ever again.
    Shepherd: That worked? 
    TinkerBell: Of course, and besides he works for one of the places we get inventory from. I just threatened to go to a competitor instead.
    Shepherd: Huh, interesting. 
    <<AddStatement TinkerBell_q1_client "TinkerBell had some type of dispute with a long standing client about non-payment ">>
<<endif>>
<<jump TinkerBell_Q1_Start>>
===
title:  TinkerBell_Q1_Accusation_Default
tags:
position: -370,-921
---
TinkerBell: And that matters how exactly? 
TinkerBell: Maybe you should just let me come back later, you seem a bit distracted.
Shepherd: Not happening, I'm fine
Shepherd: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump TinkerBell_Q1_Start>>
===
// title: TinkerBell_Q1_Accusation_Correct
// position: -260,413
// ---
// Shepherd: We noticed you had a metro card in your wallet when we booked you.
// Shepherd: We had a chat with the metro authority, and they report this card was used yesterday at King Log station, not far from your residence.
// Shepherd: So Wolf, tell me. Where did you go?
// Wolf: I...You....
// Shepherd: You better cooperate Wolf
// Wolf: Fine, I went out to do some errands. 
//<<jump TinkerBell_Q2>>
//===
title: TinkerBell_Q2
position: -760,-518
---
<<SetPose TinkerBell Smug>>
Shepherd: Alright TinkerBell, let's shift gears a bit. Do you have any connection to the Pig Family?
<<PlayEffect interesting>>
TinkerBell: I worked a bit with Momua Pig during prohibition when they smuggled moonshine. I was a bartender at one of the speakeasys they delivered to.
TinkerBell: Is that what this is about? Statute of limitations is way past by now.


<<jump TinkerBell_Q2_Start>>
===
title: TinkerBell_Q2_Start
tags:
position: -757,-330
---
<<SetPose TinkerBell Neutral>>
<<SetMusic Questions>>
<<if visited("TinkerBell_Q2_Question_3") == true && visited("TinkerBell_Q2_Question_1") == true >>
    <<jump TinkerBell_Q2>>
<<else>>
    -> Question
        -> What about her sons?
            <<jump TinkerBell_Q2_Question_1>>
        -> When was the last time you dealt with them? <<if visited("TinkerBell_Q2_Question_1") == true >>
            <<jump TinkerBell_Q2_Question_2>>
    -> Accuse
        -> This evidence proves you wrong!
            <<ResetEvidenceResponses>>
            <<SetEvidenceResponse TinkerBell_Pig_Info TinkerBell_Q2_Accusation_Correct>>
            <<PromptEvidence TinkerBell_Q2_Accusation_Default>>
    -> Review
        <<Review>>
<<endif>>

===
title: TinkerBell_Q2_Question_1
tags:
position: -1054,-207
---
<<SetPose TinkerBell Smug>>
<<if visited("TinkerBell_Q2_Question_1") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked me that, aren't you taking notes on this?
    <<jump TinkerBell_Q2_Start>>
<<else>>
    TinkerBell: Oh the boys? I've worked with them on occasion.
    Shepherd: How so?
    TinkerBell: Well, they've made some one off pyrotechnics for me and the club. Sometimes you can not find mass produced ones in the right styles and colors.
    <<PlayEffect interesting>>
    <<AddStatement TinkerBell_q2_alibi "TinkerBell has had contact with the victim and his brothers ">>
<<endif>>
<<jump TinkerBell_Q2_Start>>

===

title: TinkerBell_Q2_Question_2
position: -751,6
---
<<SetPose TinkerBell Smug>>
<<if visited("TinkerBell_Q2_Question_1") == true>>
    <<wait 0.5>>
    TinkerBell: Am I going crazy? I could have sworn you wrote that down.
    <<jump TinkerBell_Q2_Start>>
<<else>>
    TinkerBell: Hmm...honestly I don't fully remember. Gimme a sec..
    <<wait 1>>
    TinkerBell: OH, right the glitter throwing orb job!
    <<PlayEffect shock>>
    Shepherd: I'm sorry, the what??
    TinkerBell: Its another custom I ordered for the Red Hot Chili Pipers show. Its a massive sphere that hangs over the crowd and blows smoke and sparkles all over.
    Shepherd: Right...so when did you chat with them last about this?
    TinkerBell: 2 days ago.
    <<AddStatement TinkerBell_q2_client "TinkerBell has mentioned she spoke with the pigs recently.  ">>
<<endif>>
<<jump TinkerBell_Q2_Start>>
===
title:  TinkerBell_Q2_Accusation_Default
tags:
position: -412,-12
---
TinkerBell: And that matters how exactly? 
TinkerBell: Maybe you should just let me come back later, you seem a bit distracted.
Shepherd: Not happening, I'm fine
Shepherd: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump TinkerBell_Q2_Start>>
===
title: TinkerBell_Q2_Accusation_Correct
position: -419,128
---
<<SetPose TinkerBell Smug>>
Shepherd: We had a chat with the pigs the other day. They had talked about how they had a meeting with a buisness associate. 
TinkerBell: Um...ok, that matters to me how?
Shepherd: The incident we are investigating happened the same day as your chat with a troublesome client. Were you referring to the pigs?
<<PlayEffect shock>>
<<SetPose TinkerBell Neutral>>
TinkerBell: I..what? No, I never...
<<jump TinkerBell_Q3>>
===
title: TinkerBell_Q3
position: -752,239
---
<<AddCharacter MatchGirl>>
<<SetPose MatchGirl Neutral>>
MatchGirl: Oi, kid.
Shepherd: What? I'm kinda in the middle of something.
MatchGirl: I'm aware, just finished the search of her car. Here's the file for you, some real gems in there for you.
<<PlayEffect Energy>>
<<LoadEvidence TinkerBell_Search_Data>>
<<SetPose TinkerBell Angry>>
TinkerBell: Are you judging the stuff in my car? Rude. 
MatchGirl: Quiet down chartreuse bonnie. //BETTER INSULT GOES HERE MAYBE?
<<PlayEffect shock>>
MatchGirl: Alright, have fun I guess.
<<RemoveCharacter MatchGirl>>


<<jump TinkerBell_Q3_Start>>
===
title: TinkerBell_Q3_Start
tags:
position: -758,481
---
<<declare $reviewCompleted = false as bool>>
<<SetMusic Questions>>
<<SetPose TinkerBell Neutral>>
    -> Question
        -> Why all the Hydrogen Peroxide? <<if $reviewCompleted == true>>
            <<jump TinkerBell_Q3_Question_1>>
        -> Why is this still here then? <<if $reviewCompleted == true && visited("TinkerBell_Q3_Question_1") == true>>
            <<jump TinkerBell_Q3_Question_2>>
    -> Accuse
        -> This evidence proves you wrong!
            <<ResetEvidenceResponses>>
            <<SetEvidenceResponse TinkerBell_Fullbody_Mugshot TinkerBell_Q3_Accusation_Correct>>
            <<PromptEvidence TinkerBell_Q3_Accusation_Default>>
    -> Review
        <<set $reviewCompleted to true>>
        <<Review>>

===
title: TinkerBell_Q3_Question_1
tags:
position: -960,684
---
<<SetPose TinkerBell Smug>>
<<if visited("TinkerBell_Q3_Question_1") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked me that, aren't you taking notes on this?
    <<jump TinkerBell_Q3_Start>>
<<else>>
    <<PlayEffect interesting>>
    TinkerBell: The what? 
    Shepherd: Says here on the catalog from the search that we found a 5 gallon bucket of 70% hydrogen peroxide.
    Shepherd: Ok first of all, who the hell sold you gallons of industrial grade hydrogen peroxide? And second why the hell do you have it?
    TinkerBell: Well first, I got it from one of those bulk restaurant supply places. 
    Shepherd: But why do you need that much to begin with? What are you even doing with it?
    <<PlayEffect Energy>>
    TinkerBell: Impatient much? As I was saying before being <style="emph">so rudely</style> interrupted, I use it in the club for cleaning.
    Shepherd: Cleaning in general?
    TinkerBell: Sort of, we dilute it down and use if to clean bodily fluids that show up. Unfortunately it has a tendency to discolor and bleach things if not diluted enough.
    Shepherd: I see.
    <<AddStatement TinkerBell_Q3_Peroxide "TinkerBell uses high grade hydrogen peroxide for cleaning bodily fluids, but it has a tendency to bleach cloth if not diluted enough. ">>
<<endif>>
<<jump TinkerBell_Q3_Start>>

===

title: TinkerBell_Q3_Question_2
position: -541,694
---
<<if visited("TinkerBell_Q3_Question_2") == true>>
    <<SetPose TinkerBell Smug>>
    <<wait 0.5>>
    TinkerBell: Again? Are you even listening?
    Shepherd: Quiet, I'm asking the questions here.
<<endif>>
    TinkerBell: I mean, is it that important why its in my car? I think its pretty irrelevant.
    Shepherd: Just answer the question.
    TinkerBell: I must have forgotten it after doing the inventory run.
    Shepherd: How could you have forgotten it? Report says it was sitting in the passenger footwell.
    <<SetPose TinkerBell Annoyed>>
    TinkerBell: I just forgot OK? I did not know that having stuff in your car was a crime now.
<<jump TinkerBell_Q3_Start>>
===
title:  TinkerBell_Q3_Accusation_Default
tags:
position: -430,310
---
TinkerBell: And that matters how exactly? 
TinkerBell: Maybe you should just let me come back later, you seem a bit distracted.
Shepherd: Not happening, I'm fine
Shepherd: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump TinkerBell_Q3_Start>>
===

title: TinkerBell_Q3_Accusation_Correct
position: -751,715
---
<<SetPose TinkerBell Neutral>>
TinkerBell: Um...why are you showing me my mugshot? 
Shepherd: Just out of curiosity, what's up with your boots?
TinkerBell: What do you mean about my boo....ts...
<<PlayEffect shock>>
<<SetPose TinkerBell Scared>>
TinkerBell: Is this a lighting trick why the hell are they colored weird?
Shepherd: Did you by chance have an encounter with your bucket of peroxide?
<<PlayEffect interesting>>
TinkerBell: What? No I...god damn it my poor boots...they're ruined.
<<jump TinkerBell_Q4_Start>>
===
title: TinkerBell_Q4_Start
tags:
position: -763,955
---
<<SetMusic Questions>>
<<SetPose TinkerBell CheckedOut>>
    -> Question
        -> What's with the bat? 
            <<jump TinkerBell_Q4_Question_1>>
        -> Do you expect me to believe that? <<if visited("TinkerBell_Q4_Question_1") == true>>
            <<jump TinkerBell_Q4_Question_2>>
    -> Accuse
        -> This evidence proves you wrong!
            <<ResetEvidenceResponses>>
            <<SetEvidenceResponse TinkerBell_Pig_Autopsy TinkerBell_Q4_Accusation_Correct>>
            <<PromptEvidence TinkerBell_Q4_Accusation_Default>>
    -> Review
        <<Review>>

===
title: TinkerBell_Q4_Question_1
tags:
position: -970,1027
---
<<SetPose TinkerBell Flippant>>
<<if visited("TinkerBell_Q4_Question_1") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked me that, aren't you taking notes on this?
    <<jump TinkerBell_Q4_Start>>
<<else>>
    TinkerBell: I'm not really sure what you're talking about.
    Shepherd: I have to wonder, why do you have a bat in your car as well?
    TinkerBell: I play softball, why do you keep thinking everything in my car is suspicious?
    Shepherd: Just many things are beginning to click, and I wonder if you recognize how deep of a hole you are digging youself.
    <<PlayEffect interesting>>
    TinkerBell: You haven't even told me why I am here, I have no clue why you have me here.
    <<AddStatement TinkerBell_Q4_Bat "TinkerBell has a baseball bat in her car, she claims it's for softball">>
<<endif>>
<<jump TinkerBell_Q4_Start>>

===

title: TinkerBell_Q4_Question_2
position: -558,1008
---
<<if visited("TinkerBell_Q4_Question_2") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked me that, aren't you taking notes on this?
    <<jump TinkerBell_Q4_Start>>
<<endif>>
    TinkerBell: Its for softball, I swear to Jupiter.
    TinkerBell: Why won't you believe me?
    Shepherd: If its for sport reasons, why does it say "Gaslight" on it?
    <<PlayEffect shock>>
    TinkerBell: Am I not allowed to personalize? Why are you maike me out to be a criminal?
    Shepherd: Why does it also appear to be bleached? Paints all splotchy, Is it just personalized like that too?
    TinkerBell: I...
     <<AddStatement TinkerBell_Q4_Bat2 "Baseball bat has Gaslight engraved on it. The paint is extremely splotchy, as if it had been bleached ">>
<<jump TinkerBell_Q4_Start>>
===
title:  TinkerBell_Q4_Accusation_Default
tags:
position: -304,1008
---
TinkerBell: And that matters how exactly? 
TinkerBell: Maybe you should just let me come back later, you seem a bit distracted.
Shepherd: Not happening, I'm fine
Shepherd: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump TinkerBell_Q4_Start>>
===

title: TinkerBell_Q4_Accusation_Correct
position: -767,1109
---
<<PlayEffect Emphasis>>
Shepherd: TinkerBell, do you recognize why I am questioning you?
TinkerBell: I..I don't know.
Shepherd: This is the autopsy report for PIG NAME HERE. His brothers mentioned that he had been planning to meet with you.
Shepherd: Their interrogations heavily implied that you were responsible. Yours so far has only strengthened our theory.
<<PlayEffect shock>>
TinkerBell: He's....he....I...killed him? No..nonononono..
TinkerBell: Oh my gods oh my gods the boss is going to kill me...he'll find and kill me. I need to leave the country, go somewhere he won't find me.
<<PlayEffect Energy>>
TinkerBell: Oh woe! Woe on me, for I have murdered a friend!
Shepherd: TinkerBell, drop the act. You clearly had no problem beating someone near to death. I think you know you killed him, and you just want to get out of this. 
TinkerBell: Ugh, fine. I'll get you information, guarentee my safety and get me a plea deal and we'll talk.
Shepherd: You're unbearable, fine I'll get legal working on that.
<<PlayEffect ToWhite>>
<<wait 5>>
<<PlayEffect Reveal>>
Shepherd: Alright, legal's working on it now, you gonna cooperate?
TinkerBell: I suppose
<<jump TinkerBell_Q5_Start>>
===
title: TinkerBell_Q5_Start
tags:
position: -777,1287
---
<<SetMusic Questions>>
<<SetPose TinkerBell Smug>>
<<if visited("TinkerBell_Q5_Question_1") == true && visited("TinkerBell_Q5_Question_2") == true && visited("TinkerBell_Q5_Question_3") == true  && visited("TinkerBell_Q5_Question_4") == true>>
<<jump TinkerBell_Q6>>
<<endif>>
    -> Question
        -> The boss? 
            <<jump TinkerBell_Q5_Question_1>>
        -> What was your intended goal?
            <<jump TinkerBell_Q5_Question_2>>
        -> What do you know of this new boss? <<if visited("TinkerBell_Q5_Question_1") == true>>
            <<jump TinkerBell_Q5_Question_3>>
        -> Can you describe the series of events? <<if visited("TinkerBell_Q5_Question_2") == true>>
            <<jump TinkerBell_Q5_Question_4>>
    -> Accuse
        -> This evidence proves you wrong!
            <<ResetEvidenceResponses>>
            //<<SetEvidenceResponse TinkerBell_Pig_Autopsy TinkerBell_Q5_Accusation_Correct>>
            <<PromptEvidence TinkerBell_Q5_Accusation_Default>>
    -> Review

        <<Review>>

===
title: TinkerBell_Q5_Question_1
tags:
position: -1061,1423
---
//<<SetPose TinkerBell Flippant>>
<<if visited("TinkerBell_Q5_Question_1") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked, each dumb question makes me want to answer less.
<<else>>
Shepherd: You mean Wolf? Hes in our custody at this moment.
TinkerBell: Who?
<<PlayEffect interesting>>
Shepherd: Wolf..Isn't he the new boss?
TinkerBell: I don't know, I've never really talked to him and he only recently took charge around here.
<<wait 1>>
TinkerBell: Oh gods I'm dead I'm so dead I just had to rough him up a bit
<<PlayEffect shock>>
Shepherd: Oh not this act again, we are working on the plea so can you just drop the act?
TinkerBell: Maybe, I don't know yet.

<<AddStatement TinkerBell_Q5_Boss1 "TinkerBell has no clue who Wolf is. Is he really the boss? I should talk with him again.">>
<<endif>>
<<jump TinkerBell_Q5_Start>>

===

title: TinkerBell_Q5_Question_2
position: -374,1432
---
<<if visited("TinkerBell_Q5_Question_2") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked me that, my time is valuable you know?
    <<jump TinkerBell_Q5_Start>>
<<endif>>
    TinkerBell: My goal? To rise up and become the queen of crime in this hellhole of a city.
    Shepherd: <int> Hmm, wonder if Matches could grab me that bat from TinkerBell's car? <int>
    Shepherd: You know what I mean, why did you go kill the pig?
    TinkerBell: I was just supposed to rough him up a bit after the boss left.
    Shepherd: Why?
    TinkerBell: They were behind in production. They only delivered half of what they promised. And so, they did need to answer for their shortcomings. Would have been happier if he did not die however.
     <<AddStatement TinkerBell_Q5_OrginalGoal "Pigs were supposed to deliver more Pixie Dust than they did. TinkerBell was sent by boss to intimidate the Pigs. The Boss also left before TinkerBell attacked the pig">>
<<jump TinkerBell_Q5_Start>>
===
title: TinkerBell_Q5_Question_3
tags:
position: -991,1605
---
//<<SetPose TinkerBell Flippant>>
<<if visited("TinkerBell_Q5_Question_3") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked me that, my time is valuable you know?
    <<jump TinkerBell_Q5_Start>>
<<else>>
    TinkerBell: I dont know much. He's new in the area. 
    Shepherd: Did he take over from anyone or is this his own group.
    TinkerBell: He made it all himself, and he invited me to join after the Pigs recommended me.
    Shepherd: Have you ever seen him? Do you know what he looks like? Body shape? Anything?
    TinkerBell: Never his face...he always wears a mask. I'm not sure about body shape, I've only ever seen him in a trenchcoat and hat.
    Shepherd: Any distinguishing features or mannerisms? 
    //TinkerBell: He...he mentions fire sometimes...that if the pigs burned us we'll burn them back..
    //TinkerBell: But I think thats a common phrase..
    TinkerBell: Not that I can think of.
    <<AddStatement TinkerBell_Q5_Boss2 "Boss seems to like trenchcoats and hats. Very cautious with identifiers May have an affinity for fire based metaphors?">>
<<endif>>
<<jump TinkerBell_Q5_Start>>

===
title: TinkerBell_Q5_Question_4
tags:
position: -512,1601
---
//<<SetPose TinkerBell Flippant>>
<<if visited("TinkerBell_Q5_Question_4") == true>>
    <<wait 0.5>>
    TinkerBell: You already asked me about..that.
    <<jump TinkerBell_Q5_Start>>
<<else>>
    TinkerBell: I went to their farm with the boss, with the intent of asking about the missing product. 
    TinkerBell: Only one of the pigs was in the house. We had a chat about what had been going on, 
    TinkerBell: He promised me that they'd get it done soon, and I told him as I walked away that they were only going to be paid for the amount they delivered.
    Shepherd: I presume he did not take that well.
    <<PlayEffect interesting>>
    TinkerBell: He swung at me, so I beat him with the bat.
    Shepherd: Why did you have the bat? 
    TinkerBell: Intimidation mostly.
    Shepherd: Didn't the boss ask you to rough him up though?
    TinkerBell: He did, I had no intention of going through with it until he swung at me.
    Shepherd: Uh huh..
    Shepherd: What state was he in when you left?
    TinkerBell: He was unconscious on the floor, his head was bleeding. I figured his brothers would find him in a minute or two.
    Shepherd: But they didn't. He bled to death before they found him.
    TinkerBell: Unfortunate.
    
    <<AddStatement TinkerBell_Q5_Timeline "TinkerBell arrived at the farm, had a conversation with the victim. Victim got agitated and attacked TinkerBell, TinkerBell beat him until he was unconcious and fled">>
<<endif>>
<<jump TinkerBell_Q5_Start>>

===
title:  TinkerBell_Q5_Accusation_Default
tags:
position: -382,1279
---
TinkerBell: And why does that matter now?
Shepherd: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump TinkerBell_Q5_Start>>
===

title: TinkerBell_Q6
position: -774,1745
---
<<PlayEffect Shock>>
Shepherd: Alright. TinkerBell you are formally under arrest for the murder of INSERT NAME HERE. 
Shepherd: You have the right to remain silent. Anything you say can be used against you in a court of law. You have the right to have an attorney present now and during any future questioning. If you cannot afford an attorney, one will be appointed for you at no cost.

===
