title: TinkerBell
tags:
position: -749,-1282
---

===
title: TinkerBell_1
tags:
position: -748,-1095
---
<<SetMusic Chill>>
//<<SetPose TinkerBell Flippant>>
<<AddCharacter TinkerBell>>
Boy: Ah, so you must be TinkerBell.
TinkerBell: Oh so you're the Wolf Boy huh? Bit scruffier and scrawnier than I expected.

<<jump TinkerBell_Q1_Start>>
===
title: TinkerBell_Q1_Start
tags:
position: -749,-923
---
<<SetPose TinkerBell CheckedOut>>
<<if visited("TinkerBell_Q1_Question_2") == true && visited("TinkerBell_Q1_Question_1") == true >>
    <<jump TinkerBell_Q2>>
<<else>>
    -> Question
        -> This is a new low, even for you. Selling illicit drugs to kids? What's with the shift here TinkerBell?
            <<jump TinkerBell_Q1_Question_1>>
        -> This stuff has been spreading like wildfire all over the city. Where'd you get the connections to set up a distribution network like this?
            <<jump TinkerBell_Q1_Question_2>>
    -> Accuse
        -> This evidence proves you wrong!
            <<ResetEvidenceResponses>>
            //<<SetEvidenceResponse testPaper_1 TestPaper1Response>>
            <<PromptEvidence TinkerBell_Q1_Accusation_Default>>
    -> Review
        <<Review>>
<<endif>>

===
title: TinkerBell_Q1_Question_1
tags:
position: -946,-784
---
<<SetPose TinkerBell Flippant>>
<<if visited("TinkerBell_Q1_Question_1") == true>>
    <<wait 0.5>>
    TinkerBell: Again? Did you forget what I said the first time? Check that little book of yours.
<<else>>
    //Isn't TinkerBell trying to deny he sells drugs?
    TinkerBell: Wow. Great question, Sherlock. Money, Power whatever really...
    <<AddStatement TinkerBell_q1_drugs "TinkerBell sells drugs for the money and power">>
<<endif>>
Boy: Alright, smart ass.
<<jump TinkerBell_Q1_Start>>

===
title: TinkerBell_Q1_Question_2
tags:
position: -538,-785
---
<<SetPose TinkerBell CheckedOut>>
<<if visited("TinkerBell_Q1_Question_2") == true>>
    <<SetPose TinkerBell Flippant>>
    <<wait 0.5>>
    TinkerBell: Again? Did you forget what I said the first time?
    Boy: I'm asking the questions here.
<<endif>>
TinkerBell: Simple, law of supply and demand. Demand for this stuff went up, so everyone came to the only supply: Me.
Boy: What are you even calling this stuff? Every person we've talked to has a different name for it.
TinkerBell: Pixie Dust, name is stupid I know..
Boy: But didn't you...ah whatever
<<jump TinkerBell_Q1_Start>>

===
title:  TinkerBell_Q1_Accusation_Default
tags:
position: -323,-926
---
TinkerBell: And thats relevant how? 
TinkerBell: You're losing your edge kid
Boy: <style="int">(You can hear Match Girl's voice "Theres a time and place for everything, but not now")</style>
<<jump TinkerBell_Q1_Start>>
===
title: TinkerBell_Q2
position: -745,-569
---
<<SetPose TinkerBell CheckedOut>>
Boy: Alright TinkerBell, I have a few more questions for you. 
TinkerBell: Sure, not like I can go anywhere. Can ya hurry this up, I have a shift in a few hours?
Boy: Depends on how you answer these questions TinkerBell, don't lie or mouth off and it will be done sooner.

<<jump TinkerBell_Q2_Start>>
===