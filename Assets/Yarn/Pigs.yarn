title: Pigs
---

//ADD EVIDENCE
<<Checkpoint Pigs>>
<<PlayEffect Reveal>>
<<RemoveAllEvidence>>
<<LoadEvidence Pigs_Autopsy>>
<<LoadEvidence Pigs_FamilyPortrait>>
<<LoadEvidence Pigs_Crime_Scene>>
<<LoadEvidence Pigs_Farm_Map>>
<<LoadEvidence Pigs_Tax_Returns>>
<<ReviewWithDestination PigsStart>>
<<ResetLives>>
<<ResetStatements>>
<<SetLoseNode PigsLose>>

===

title: PigsStart
tags:
position: 122,-203
---
Shepherd: Alright, I'm ready. Send them in.

<<RemoveCharacter MatchGirl>>
<<wait 1.5>>
<<AddCharacter Bricks>>
<<SetPose Bricks neutral_sitting>>
<<wait 1>>
<<PlayEffect Interesting>>
<<AddCharacter Straw>>
<<SetPose Straw neutral_idle>>
<<wait 1>>
Shepherd: ...Good to meet you, gentlemen. I'm very sorry for what happened.
Shepherd: I'm Detective Shepherd, and I just need to ask you a few questions about this morning.
Bricks: I appreciate your work, detective. My name's Bricks, and this here is Straw.
Straw: ...*hic*...
Bricks: Pardon my brother, he's... not havin' a good day.
Shepherd: <style="int">(Oooookay.)</style>
Shepherd: ...I'm sure you're very torn up by the death of Sticks-
<<StopMusic>>
<<PlayEffect Emphasis>>
<<SetPose Straw sad_wailing>>
//Crying pose???
Straw: STIIIIIIIICKKSSSSS!!-
<<PlayEffect Energy>>
Bricks: I, I'm sorry, Straw is just so torn up- could we, ah, maybe avoid sayin'-
<<SetMusic Chill>>
Shepherd: Of course, I understand. These things are hard.
<<SetPose Straw sad_crying>>
Straw: *sniffle*
Shepherd: <style="int">(Oh, boy. The younger one seems a bit... unstable. I should be gentle with my questioning.)</style>
<<AddStatement Pigs_Straw "Straw seems a bit unstable and extremely out of it.">>

<<jump Pigs_Q1_Start>>

//Pigs Q1
===
title: Pigs_Q1_Start
tags:
position: 123,-136
---
<<AddCharacter Bricks>>
<<AddCharacter Straw>>
<<SetMusic Questions>>
<<if visited("Pigs_Q1_Question_2") == true && visited("Pigs_Q1_Question_1") == true >>
    <<jump Pigs_Q2_Start>>
<<else>>
    -> Question
        -> What was your relationship like with Sticks?
            <<jump Pigs_Q1_Question_1>>
        -> What were you doing at the time of the murder?
            <<jump Pigs_Q1_Question_2>>
        //-> Did either of you see anyone enter or leave? <<if visited("Pigs_Q1_Question_2") == true && visited("Pigs_Q1_Question_1") == true>>
           // <<jump Pigs_Q1_Question_3>>
    -> Accuse
        -> You're lying about this statement!
            <<ResetEvidenceResponses>>
            <<PromptStatement Pigs_Q1_Accusation_Default>>
    -> Review
        <<Review>>
<<endif>>
===
title: Pigs_Q1_Question_1
tags:
position: 60,-13
---
Shepherd: So, what was your relationship like with Sticks?
<<wait 0.5>>
<<PlayEffect Emphasis>>
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw sad_crying>>
Straw: S-STICKS! H-he's... Bricks... I-
<<SetPose Bricks angry_sitting>>
Bricks: Detective! I told you not to bring him up!
Shepherd: I'm sorry! It's very difficult to investigate a murder without-
Straw: A-a murder?! B-bricks...! D-don't tell me...
Bricks: No, no, shhh...! Sticks is, uh-
<<PlayEffect Emphasis>>
<<SetPose Straw sad_wailing>>
Straw: I'll KILL 'em! Who murdered my brother!? I'll stab 'em through the-
<<PlayEffect Emphasis>>
<<SetPose Bricks angry_idle>>
Bricks: That's enough!
<<wait 1>>
<<PlayEffect Interesting>>
 //SET POSE TO SOMETHING NEUTRAL OR GIGGLING
 <<SetPose Bricks neutral_sitting>>
 <<SetPose Straw neutral_idle>>
<<wait 2>>
//todo: answer question more maybe?
Bricks: Apologies again for my brother's decorum.
Shepherd: <style="int">(Grief is a powerful thing, huh? Seems like Straw will be like this for a while. Might want to question Bricks more.)</style>
<<jump Pigs_Q1_Start>>
===
title: Pigs_Q1_Question_2
tags:
position: -127,-18
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: What were you doing at the time of the murder?
Bricks: I had been tending to my strawburry patch, and when I went into the house he was...
<<SetPose Straw giggle_laugh>>
Straw: Oo, strawberries! Mr. Detective, you have strawberries?
<<wait 1>>
Bricks: Ahem.
Bricks: Well, you know..
Shepherd: Straw, what were you doing while Bricks was working on his strawberry patch?
Straw: I was beatin' the tree trunk!
<<PlayEffect Shock>>
Shepherd: You what!?
<<SetPose Straw neutral_idle>>
Bricks: Oh for Pete's sake Straw, watch yer' phrasin'. Excuse him officer, He was clearin' brush for firewood.
<<wait 0.5>>
Shepherd: I..alright..Where on the property was this?
Bricks: Over on the other side of the shed, opposite of my field.
<<AddStatement Pigs_Q1_WhereTheyWere "At the time of the murder, Bricks was tending to his strawberries and Straw was clearing brush for firewood">>
<<jump Pigs_Q1_Start>>
===

title: Pigs_Q1_Accusation_Default
tags:
position: 331,-153
---
<<SetPose Bricks neutral_sitting>>
Shepherd: You're lying! And I can prove it with-
Bricks: Sir! This is hardly the time for accusations!
Shepherd: ...Sorry, you're right.
Shepherd: <style="int">(Whoops.)</style>
<<LoseLife>>
<<jump Pigs_Q1_Start>>
===
title: Pigs_Q1_Accusation_Correct
tags:
position: 334,-37
---

<<jump Pigs_Q2_Start>>


//Pigs Q2
===
title: Pigs_Q2_Start
tags:
position: 123,141
---
<<AddCharacter Bricks>>
<<AddCharacter Straw>>
-> Question
    -> During this time, what was your brother doing?
        <<jump Pigs_Q2_Question_1>>
    -> Who is this friend? <<if visited("Pigs_Q2_Question_1") == true>>
        <<jump Pigs_Q2_Question_2>>
-> Accuse
    -> You're lying about this statement!
        <<ResetEvidenceResponses>>
        //SET CORRECT EVIDENCE HERE
        <<SetStatementResponse Pigs_Q2_WhereTheyWere Pigs_Farm_Map Pigs_Q2_Accusation_Correct>>
        <<PromptStatement Pigs_Q2_Accusation_Proof>>
-> Review
    <<Review>>
===
title: Pigs_Q2_Question_1
tags:
position: 71,226
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: While you two were busy, what was your brother doing?
Bricks: Well, yesterday he told me that he was gonna be fillin' some tax reports. Something about nothin' bein' scarier than the IRS breathin' down your neck.
<<PlayEffect Interesting>>
Straw: Huh? He tolds me that he was meetin with a frien' of his.
<<AddStatement Pigs_Q2_BrotherStory "Sticks was either working on their taxes or meeting with a friend">>
<<wait 1>>
<<SetPose Straw sad_wailing>>
Straw: Wait, did this frien' o' his hurt him? I'll KILL 'em! I'll get me shotgun and blow holes in them till they look like a fuckin..
<<PlayEffect Shock>>
<<SetPose Bricks angry_sitting>>
Bricks: STRAW! SHUT YER MOUTH and calm down.
<<SetPose Straw sad_crying>>
Straw: *sniffle* Sorry, got bit carried away there.
Shepherd: <style="int">(He has a gun? How the hell did someone like Straw pass the mental exam to buy one?)</style>
Shepherd: <style="int">(Not my problem now, I'll call ATF later. For now, that story difference is strange.)</style>

<<jump Pigs_Q2_Start>>
===
title: Pigs_Q2_Question_2
tags:
position: -138,219
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: Do you have any idea who this "friend" is?
Bricks: No clue, I didn't even know Sticks had any friends. 
Shepherd: And if I rember correctly neither of you saw anyone enter or leave?
Bricks: That's right, neither of us were in a spot where we could see the house.
Shepherd: <style="int">(Is that even possible?)</style> //
<<AddStatement Pigs_Q2_WhereTheyWere "At the time of the murder, Bricks was tending to his strawberries and Straw was clearing brush for firewood">>
<<jump Pigs_Q2_Start>>
===
title: Pigs_Q2_Accusation_Proof
tags:
position: 534,183
---
Bricks: Yeah? and what's your proof?
<<PromptEvidence Pigs_Q2_Accusation_Default>>
===
title: Pigs_Q2_Accusation_Default
tags:
position: 348,109
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: You're lying! And I can prove it with-
Bricks: Sir! This is hardly the time for accusations!
Shepherd: ...Sorry, you're right.
Shepherd: <style="int">(Whoops.)</style>
<<LoseLife>>
<<jump Pigs_Q2_Start>>
===
title: Pigs_Q2_Accusation_Correct
tags:
position: 350,186
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: You both say that you were in places where you could not see the house
Bricks: Yeah, so?
Shepherd: Looking at the map of the property, the spots you both claim to have been either had a sightline to the house or the driveway out
<<PlayEffect Energy>>
Bricks: Are you callin' me a liar? Them's fightin' words.
Straw: Yeah, fightin' words.
Shepherd: I'm not calling either of you liars, but neither of you are telling me the truth.
Shepherd: So how about we start again?
Bricks: Nah, we aint gonna change our stories
Shepherd: Fine then, lets come back to this.
Shepherd:<style="int">(I need to be careful, these two seem liable to get violent)</style>
<<jump Pigs_Q3_Start>>

//Pigs Q3
===
title: Pigs_Q3_Start
tags:
position: 124,340
---
<<AddCharacter Bricks>>
<<AddCharacter Straw>>
<<SetMusic QuestionsFaster>>
<<declare $reviewCompletedPigs = false as bool>>
-> Question
    -> What are your finacials like?
        <<jump Pigs_Q3_Question_1>>
    -> How do you both earn most of your money? <<if visited("Pigs_Q3_Question_1") == true>>
        <<jump Pigs_Q3_Question_2>>
-> Accuse
    -> You're lying about this statement!
        <<ResetEvidenceResponses>>
        //SET CORRECT EVIDENCE HERE
        <<SetStatementResponse Pigs_Q3_Money Pigs_Tax_Returns Pigs_Q3_Accusation_Correct>>
        <<PromptStatement Pigs_Q3_Accusation_Proof>>
-> Review
    <<set $reviewCompletedPigs to true>>
    <<Review>>
===
title: Pigs_Q3_Question_1
tags:
position: 54,440
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: What are your...
<<SetPose Straw giggle_rub>>
Straw: The fuck kinda question do that be huh? 
<<PlayEffect Energy>>
<<SetPose Bricks angry_sitting>>
Bricks: Straw. Stop. NOW!
<<wait 1.5>>
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Bricks: Sorry about him, could you repeat the question? 
Shepherd: What are your finacials like?
Bricks: Well..Sticks usually handled the finances. As far as I know we are stable but rather poor.
<<AddStatement Pigs_Q3_Money "Sticks handled their finacials, though Bricks mentions they are rather poor">>
Shepherd:<style="int">(Interesting, I should check out those tax forms we found.)</style>


<<jump Pigs_Q3_Start>>
===
title: Pigs_Q3_Question_2
tags:
position: -149,435
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: How do you both earn most of your money?
Bricks: Well, I run that strawberry patch. Sell strawberries to people. 
Bricks: And Straw sells firewood from the trees on the property as well as taking the occasional odd job.
Shepherd: Speaking of strawberries, did you happen to sell any the day your brother was murdered?
<<StopMusic>>
<<PlayEffect Emphasis>>
<<SetPose Straw sad_wailing>>
Straw: STIIIIIIIICKKSSSSS!!- STIIIIIIIICKKSSSSS!!-
<<PlayEffect shock>>
<<SetPose Bricks angry_idle>>
Bricks: WHAT DID I SAY BEFORE?
<<SetMusic Chill>>
Shepherd: Shit, sorry. 
<<SetPose Bricks angry_sitting>>
Bricks: You had better be, don't you hurt my brother. Fuckin' pig.
Shepherd:<style="int">(Large stone to be throwing from your glass house pork chop)</style>
<<SetPose Straw sad_crying>>
Straw: *sniffle*
Shepherd: Can you answer the question?
Bricks: No.
<<if $reviewCompletedPigs == true>>
Shepherd:<style="int">(Hmm, that's really weird. Those tax forms had a huge declared income and business deductions on them)</style>
<<endif>>
<<jump Pigs_Q3_Start>>
===
title: Pigs_Q3_Accusation_Proof
tags:
position: 543,331
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Bricks: Yeah? and what's your proof?
<<PromptEvidence Pigs_Q3_Accusation_Default>>
===
title: Pigs_Q3_Accusation_Default
tags:
position: 350,333
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: You're lying! And I can prove it with-
Bricks: Sir! This is hardly the time for accusations!
Shepherd: ...Sorry, you're right.
Shepherd: <style="int">(Whoops.)</style>
<<LoseLife>>
<<jump Pigs_Q3_Start>>
===
title: Pigs_Q3_Accusation_Correct
tags:
position: 353,432
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_lookingDown>>
Shepherd: You mentioned you all were relatively poor correct?
Bricks: Thats what I said, yeah.
<<PlayEffect Emphasis>>
<<SetMusic Contradiction>>
Shepherd: Then why do your tax forms declare an income this year of 1.8 million?
Bricks: I..I don't..
<<PlayEffect Energy>>
<<SetPose Straw giggle_laugh>>
Straw: Oh that's easy, its all because of the basement!
<<PlayEffect Emphasis>>
<<SetPose Bricks angry_sitting>>
Bricks: STRAW! SHUT IT. NOW.
<<wait 2>>
Shepherd: Straw, what's in the basement?
<<SetPose Bricks neutral_sitting>>
Bricks: Ignore him officer, he's an idiot who makes things up all the time. Right Straw?
<<StopMusic>>
<<SetPose Straw giggle_rub>>
Straw: Hehe yeah.
<<AddStatement Pigs_Basement "There is something in that basement that they both want to hide that makes them a lot of money.">>
Shepherd: How about we switch gears a bit? Straw, try not to listen to what your brother and I are saying.
Straw: Hehe okie dokie!
Shepherd:<style="int">(No clue what's up with him. Hope he's not listening to this next part)</style>

<<jump Pigs_Q4_Start>>


//Pigs Q4
===
title: Pigs_Q4_Start
tags:
position: 125,558
---
<<AddCharacter Bricks>>
<<AddCharacter Straw>>
<<SetMusic Answers>>
-> Question
    -> What happened when you discovered Sticks?
        <<jump Pigs_Q4_Question_1>>
    -> Do you think this person he met with killed him?
        <<jump Pigs_Q4_Question_2>>
-> Accuse
    -> You're lying about this statement!
        <<ResetEvidenceResponses>>
        //SET CORRECT EVIDENCE HERE
        <<SetStatementResponse Pigs_Q4_Scene Pigs_Crime_Scene Pigs_Q4_Accusation_Correct>>
        <<PromptStatement Pigs_Q4_Accusation_Proof>>
-> Review
    <<Review>>
===
title: Pigs_Q4_Question_1
tags:
position: 16,629
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: What happened when you discovered Sticks?
<<wait 1>>
Bricks: I walked into the house and saw him on the floor.
Shepherd: Did you attempt first aid or did you immediately call us?
Bricks: I tried first aid, but after a few seconds I saw I couldn't do nothin' for him and called.
<<PlayEffect Interesting>>
<<AddStatement Pigs_Q4_Aid "Brick attempted first aid on Sticks before calling 911">>
Shepherd: After you called, what did you do next?
Bricks: I ran outside to wait for the paramedics to arrive.
Shepherd: Did anything strike you as strange inside? We're there any clues about the other party?
<<wait 2>>
Bricks: No.
<<AddStatement Pigs_Q4_Scene "Brick reports no strange items or clues about the other party inside the house">>
<<jump Pigs_Q4_Start>>
===
title: Pigs_Q4_Question_2
tags:
position: -189,621
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_lookingDown>>
Shepherd: Do you think this person he met with killed him?
<<PlayEffect Energy>>
<<SetPose Bricks angry_sitting>>
Bricks: 100%, that bitch killed him.
<<wait 0.5>>
<<PlayEffect Interesting>>
Shepherd: But didn't you say earlier you don't recall anyone coming or leaving before?
<<SetPose Bricks neutral_sitting>>
Bricks: I..I was speaking generally
Shepherd: You know who killed him, don't you?
Bricks: Nope, no idea.
<<jump Pigs_Q4_Start>>
===
title: Pigs_Q4_Accusation_Proof
tags:
position: 536,544
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Bricks: Yeah? and what's your proof?
<<PromptEvidence Pigs_Q4_Accusation_Default>>
===
title: Pigs_Q4_Accusation_Default
tags:
position: 354,544
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: You're lying! And I can prove it with-
Bricks: Sir! This is hardly the time for accusations!
Shepherd: ...Sorry, you're right.
Shepherd: <style="int">(Whoops.)</style>
<<LoseLife>>
<<jump Pigs_Q4_Start>>
===
title: Pigs_Q4_Accusation_Correct
tags:
position: 367,620
---
<<StopMusic>>
Shepherd: The crime scene description mentions bloody foot prints matching some type of high heel leading to the door.
Shepherd: It seems like you have both a good understanding of what happened and no idea what happened.
Bricks: I have no idea what happened.
Shepherd: Bricks, what's in the basement?
Bricks: I have no clue what you're talking about.
<<jump Pigs_Q5_Start>>

//Pigs Q5
===
title: Pigs_Q5_Start
tags:
position: 112,787
---


// <<LoadEvidence Pigs_Autopsy>>
// <<LoadEvidence Pigs_FamilyPortrait>>
// <<LoadEvidence Pigs_Crime_Scene>>
// <<LoadEvidence Pigs_Farm_Map>>
// <<LoadEvidence Pigs_Tax_Returns>>
// <<AddCharacter Straw>>
// <<AddCharacter Bricks>>
<<AddCharacter Bricks>>
<<AddCharacter Straw>>
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: <style="int">(I have an idea what's there but I need the right piece of note and evidence..)</style>
<<ResetEvidenceResponses>>
//SET CORRECT EVIDENCE HERE
<<SetStatementResponse Pigs_Straw Pigs_Tax_Returns Pigs_Q5_Accusation_Correct>>
<<PromptStatement Pigs_Q5_Accusation_Proof>>
===
title: Pigs_Q5_Accusation_Proof
tags:
position: 536,544
---
<<PromptEvidence Pigs_Q5_Accusation_Default>>
===
title: Pigs_Q5_Accusation_Default
tags:
position: 354,544
---
<<PlayEffect Failure>>
Shepherd: <style="int">(Damn it, that's not it. Lost my train of thought.)</style>
<<LoseLife>>
<<jump Pigs_Q5_Start>>
===
title: Pigs_Q5_Accusation_Correct
tags:
position: 363,884
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_lookingDown>>
Shepherd: These tax returns imply a very lucrative business is happening at your farm, presumably in the basement.
<<wait 0.5>>
Shepherd: Add in how secretive you both seem to be about about that basement...
<<wait 0.5>>
Shepherd: And add how Straw has been acting. Maybe I'm a bit off but...
<<wait 1>>
<<PlayEffect shock>>
<<SetMusic Contradiction>>
Shepherd: Is there a drug lab in your basement?
<<StopMusic>>
<<wait 4>>
<<SetPose Bricks scared_looking>>
<<SetPose Straw giggle_rub>>
Straw: Uh huh! Wanna try a new pixie dust batch with me?
<<SetMusic Questions>>
<<wait 0.5>>
<<PlayEffect shock>>
<<SetPose Bricks angry_idle>>
Bricks: FOR FUCK'S SAKE STRAW! Why the hell did you tell him about that?
<<SetPose Straw neutral_lookingDown>>
Shepherd: Judging from your reaction, Bricks, I'm right aren't I?
<<SetPose Bricks neutral_sitting>>
Bricks: I...
Shepherd: We already have enough evidence to get a warrant to search your property, so how about you save us all some time?
Bricks: ...
<<wait 1>>
Bricks: There's a lab under the shed...
Shepherd: <style="int">(Bricks really should have stayed silent, then I would have ignored Straws)</style>
<<jump Pigs_Q6_Start>>

//Pigs Q6
===
title: Pigs_Q6_Start
tags:
position: 111,994
---
<<AddCharacter Bricks>>
<<AddCharacter Straw>>
<<SetMusic Trepidation>>
-> Question
    -> So, what's the actual story?
        <<jump Pigs_Q6_Question_1>>
    -> This "friend", who are they?
        <<jump Pigs_Q6_Question_2>>
-> Accuse <<if visited("Pigs_Q6_Question_2") == true>>
    -> Take a look at this.
        <<ResetEvidenceResponses>>
        <<SetEvidenceResponse Pigs_FamilyPortrait Pigs_Q6_Accusation_Correct>>
        <<PromptEvidence Pigs_Q6_Accusation_Default>>
-> Review
    <<Review>>
===
title: Pigs_Q6_Question_1
tags:
position: 49,1112
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_lookingDown>>
Shepherd: So, what's the actual story?
Bricks: We..we were in the lab. Straw was trying a new batch, and I was packin' it all up. 
Shepherd: Uh huh.
Bricks: I came upstairs after and started tendin' to my strawberries. I think I talked to someone.
Shepherd: You think?
Bricks: I inhaled some pixie dust and so I was kinda high. While trippin' i think i talked to someone but I can't recall if it was real or not.
<<if visited("Pigs_Q6_Question_2") == false>>
    Shepherd: <style="int">(Who could that have been if it was real, Wolf? But why would he double back if he killed Sticks)</style>
<<else>>
     Shepherd: <style="int">(Was that Wolf? But if he did kill Sticks why would he have stayed around for so long? Build an alibi maybe?)</style>
<<endif>>
<<jump Pigs_Q6_Start>>
===
title: Pigs_Q6_Question_2
tags:
position: -168,1117
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_lookingDown>>
Shepherd: This "friend", who are they?
<<wait 0.5>>
<<SetPose Bricks scared_looking>>
Bricks: I..I can't say.
Shepherd: Why not?
Bricks: They...they scare me.
<<SetPose Straw neutral_idle>>
Straw: If we talk we might end up in trouble.
Shepherd: <style="int">(I need to convince them to talk, and tell me who they suspect it was.)</style>
<<jump Pigs_Q6_Start>>
===
title: Pigs_Q6_Accusation_Proof
tags:
position: 518,1092
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Bricks: Yeah? and what's your proof?
<<PromptEvidence Pigs_Q6_Accusation_Default>>
===
title: Pigs_Q6_Accusation_Default
tags:
position: 328,1092
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Bricks: Yeah, what about it? Why should that do anything for me?
Shepherd: <style="int">(Damn it, that's not it.)</style>
<<LoseLife>>
<<jump Pigs_Q6_Start>>
===
title: Pigs_Q6_Accusation_Correct
tags:
position: 337,1203
---
<<SetPose Bricks neutral_sitting>>
<<SetPose Straw neutral_idle>>
Shepherd: Both of you, look at this picture.
Shepherd: Your brother is dead, and you both seem to have an idea who did it.
Shepherd: Don't you want to get revenge on them? For your brother?
<<wait 3>>
<<SetPose Straw sad_wailing>>
<<PlayEffect Interesting>>
Straw: TinkerBell!!!
Shepherd: TinkerBell? Who's that?
<<SetPose Bricks angry_sitting>>
Bricks: Our distibutor and the boss' right hand.
Bricks: She was supposed to meet with Sticks to discuss distribution.
Shepherd: Any reason she would have to murder him?
Bricks: ...
Bricks: We were dealing on the side...
Straw: We jus' wanted a little more!
Bricks: It's possible the boss found out and wanted to teach us a lesson.
Shepherd: And this "boss"... who is that?
Bricks: ...I don't know. We've only dealt with TinkerBell.
Shepherd:... Alright. Thank you for your cooperation.
<<SetPose Straw sad_crying>>
Straw: Bring her down!
Bricks: For our brother, for Sticks.
<<PlayEffect ToWhite>>
<<jump Pigs_Ending>>

===
title: Pigs_Ending
tags:
position: 58,1403
---
<<SetMusic Chill>>
<<wait 5>>
<<RemoveCharacter Bricks>>
<<RemoveCharacter Straw>>
<<AddCharacter MatchGirl>>
<<SetPose MatchGirl neutral_idle>>
<<PlayEffect Reveal>>
Matches: TinkerBell huh?
Shepherd: Thats what they said.
Matches: You ever heard of her? Who is she?
Shepherd: I think she owns a nightclub downtown, though I never expected her to be the murderous type.
Matches: Some people will always dissapoint huh? Anyways, head home Shepherd. We should have her in here at some point tomorrow morning to have a chat with you.
Shepherd: Thanks Matches, really appreciate it.
<<PlayEffect ToBlack>>
<<wait 5>>
<<StopMusic>>
<<RemoveCharacter MatchGirl>>

<<jump StartTinkerBell>>
===
title: PigsLose
---
Bricks: What are you trying to pull here?
Straw: Y-yeah! This is a in-pos-ition!
Bricks: ...We won't be talking anymore, detective.
Shepherd: <style="int">(Dammit! I must have pushed too hard!)</style>
<<PlayEffect ToBlack>>
<<wait 3>>
<<RemoveCharacter Wolf>>
<<jump Pigs>>
===